#autoload

(( CURRENT == 1 || compstate[nmatches] > 0 )) &&
  return 1

local lbuffer=${(b)LBUFFER%$PREFIX}
local -a lbuffer_words=( "${(@Z+C+)lbuffer}" )
local prev_word=$lbuffer_words[-1]
local -a lines=( "${(@)history[(R)*$lbuffer*]}" )
local -a line_words disp=() matches=()
local -i index
local line word
for line in $lines; do
  line_words=( "${(@Z+C+)line}" )
  (( index = $line_words[(ie)$prev_word] + 1 ))
  (( index > $#line_words )) && continue
  word=${line_words[index]}
  case $word in
    \$\'*)
      word=${(QQQQ)word}
      ;;
    \"*)
      word=${(QQQ)word}
      ;;
    \'*)
      word=${(QQ)word}
      ;;
    *)
      word=${(Q)word}
      ;;
  esac
  if (( $#word > 1 )); then
    matches+=( $word )
  fi
done
matches=( "${matches[@]:#(?|$words[CURRENT])}" )
_description $group_opt history-words expl 'history word'
compadd "$expl[@]" -fQ -a matches
