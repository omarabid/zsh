#!/bin/zsh
setopt localoptions extendedglob nobeep
local curcontext; .autocomplete.curcontext history-search

local -A keymap_historysearch=(
  $key[Left] .backward-char
  $key[Right] .forward-char
  $key[ForwardWord] .backward-word
  $key[BackwardWord] .forward-word
  $key[Home] .beginning-of-line
  $key[End] .end-of-line
)
local -A keymap_menuselect=()
local k v ret buffer=$BUFFER

BUFFER="${(j:*:)${(bZ+C+)BUFFER}}"
zle -R
{
  for k v in ${(kv@)keymap_historysearch}; do
    keymap_menuselect[$k]="$(bindkey -M menuselect -L $k)"
    bindkey -M menuselect $k $v
  done

  zle _history_search
  ret=$?
  if (( ret == 0 )); then
    setopt banghist
    zle .expand-history
  else
    BUFFER=$buffer
  fi

} always {
  for k v in ${(kv@)keymap_menuselect}; do
    if [[ $v == *'undefined key' ]]; then
      bindkey -M menuselect -r $k
    else
      eval "$v"
    fi
  done
}
return ret
